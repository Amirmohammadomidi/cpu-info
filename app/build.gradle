apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jetbrains.kotlin.android.extensions'
apply plugin: 'io.fabric'
apply from: "$rootDir/reports/jacoco.gradle"
apply from: "$rootDir/versioning.gradle"

Properties signingProperties = getReleaseSigningConfig()

android {
    compileSdkVersion Versions.compiledSdk
    buildToolsVersion Versions.buildTools

    defaultConfig {
        applicationId "com.kgurgul.cpuinfo"
        minSdkVersion Versions.minSdk
        targetSdkVersion Versions.targetSdk
        versionCode generatedVersionCode
        versionName generatedVersionName

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        releaseVersion {
            storeFile file(signingProperties.getProperty('keyPath'))
            storePassword signingProperties.getProperty('keyPass')
            keyAlias signingProperties.getProperty('keyAlias')
            keyPassword signingProperties.getProperty('keyPass')
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.releaseVersion
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            testCoverageEnabled true
            applicationIdSuffix ".debug"
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions.incremental = true

    dataBinding {
        enabled = true
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

androidExtensions {
    experimental = true
}

kapt {
    generateStubs = true
    mapDiagnosticLocations = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "com.android.support:appcompat-v7:${Versions.androidSupport}"
    implementation "com.android.support:design:${Versions.androidSupport}"
    implementation "com.android.support:preference-v14:${Versions.androidSupport}"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:${Versions.kotlin}"
    implementation "org.jetbrains.anko:anko-coroutines:${Versions.anko}"

    // Firebase
    releaseImplementation 'com.crashlytics.sdk.android:crashlytics:2.9.1'
    implementation "com.google.firebase:firebase-core:${Versions.firebase}"

    // Bus
    implementation 'org.greenrobot:eventbus:3.1.1'

    // Rx
    implementation 'io.reactivex.rxjava2:rxjava:2.1.4'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'

    // GSON
    implementation 'com.google.code.gson:gson:2.8.0'

    // Dagger
    implementation "com.google.dagger:dagger:${Versions.dagger}"
    implementation "com.google.dagger:dagger-android-support:${Versions.dagger}"
    kapt "com.google.dagger:dagger-compiler:${Versions.dagger}"
    kapt "com.google.dagger:dagger-android-processor:${Versions.dagger}"

    // Timber
    implementation 'com.jakewharton.timber:timber:4.5.1'

    // Google arch
    implementation "android.arch.lifecycle:extensions:${Versions.archLifecycle}"
    kapt "android.arch.lifecycle:compiler:${Versions.archLifecycle}"

    // Glide
    implementation "com.github.bumptech.glide:glide:${Versions.glide}"
    kapt "com.github.bumptech.glide:compiler:${Versions.glide}"

    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-core:2.11.0"
    testImplementation "com.nhaarman:mockito-kotlin-kt1.1:1.5.0"
    testImplementation "android.arch.core:core-testing:${Versions.archLifecycle}"

    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation "com.android.support.test.espresso:espresso-core:${Versions.espresso}"
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:${Versions.espresso}"
}

def getReleaseSigningConfig() {
    try {
        Properties localProp = new Properties()
        localProp.load(project.rootProject.file('local.properties').newDataInputStream())
        if (localProp.getProperty('keyPath') == null
                || localProp.getProperty('keyPass') == null
                || localProp.getProperty('keyAlias') == null) {
            return getDebugSigningConfig()
        } else {
            return localProp
        }
    } catch (FileNotFoundException ignored) {
        return getDebugSigningConfig()
    }
}

def getDebugSigningConfig() {
    logger.warn("Using default debug key instead of release key!")
    Properties properties = new Properties()
    def debugKeyDir = System.getenv('HOME') + "/.android/debug.keystore"
    properties.setProperty('keyPath', debugKeyDir)
    properties.setProperty('keyPass', 'android')
    properties.setProperty('keyAlias', 'androiddebugkey')
    properties.setProperty('keyPass', 'android')
    return properties
}

apply plugin: 'com.google.gms.google-services'
